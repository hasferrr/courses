// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=zx1);
    Not16(in=zx1, out=nx1);
    Mux16(a=zx1, b=nx1, sel=nx, out=nx2);

    Mux16(a=y, b=false, sel=zy, out=zy1);
    Not16(in=zy1, out=ny1);
    Mux16(a=zy1, b=ny1, sel=ny, out=ny2);

    Add16(a=nx2, b=ny2, out=add);
    And16(a=nx2, b=ny2, out=and);
    Mux16(a=and, b=add, sel=f, out=fxy);
    Not16(in=fxy, out=notfxy);

    Mux16(a=fxy, b=notfxy, sel=no, out=out0);

    // out
    Not16(in=out0, out=notout0);
    Not16(in=notout0, out=out);
    Not16(in=notout0, out[15]=forng);

    // zr
    Not16(in=notout0, out[0..7]=forzr1);
    Not16(in=notout0, out[8..15]=forzr2);
    Or8Way(in=forzr1, out=orzr1);
    Or8Way(in=forzr2, out=orzr2);
    Or(a=orzr1, b=orzr2, out=zr3);
    Not(in=zr3, out=zr);

    // ng
    Mux(a=false, b=true, sel=forng, out=ng);
}

